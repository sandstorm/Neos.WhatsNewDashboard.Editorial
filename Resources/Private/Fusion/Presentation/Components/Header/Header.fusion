prototype(Flowpack.Neos.WhatsNewDashboard.Editorial:Component.Header) < prototype(Neos.Fusion:Component) {
    headline = ''
    introText = ''
    menuEntries = null

    @propTypes {
        @strict = true
        headline = ${PropTypes.string}
        introText = ${PropTypes.string}
        menuEntries = ${PropTypes.arrayOf(PropTypes.string)}
    }

    renderer = afx`
        <header class="grid grid-cols-1 sm:grid-cols-2 gap-10 pb-10 sm:pb-16 border-b border-b-grey-light">
            <div class="flex flex-col justify-between">
                <div class="[&>*+*]:mt-10">
                    <h1>{props.headline}</h1>
                    <div class="text-3xl">{props.introText}</div>
                </div>
            </div>
            <div class="[&>*+*]:mt-10">
                <h2>
                    Quicklinks<br />
                    Last updates
                </h2>
                <nav>
                    <ul>
                        <Neos.Fusion:Loop items={props.menuEntries}>
                            <li><a href={'#' + String.replace(String.toLowerCase(item), ' ', '-')}>{item}</a></li>
                        </Neos.Fusion:Loop>
                    </ul>
                </nav>
            </div>
        </header>

    `
}
