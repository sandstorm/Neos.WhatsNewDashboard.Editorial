prototype(Flowpack.Neos.WhatsNewDashboard.Editorial:Document.WhatsNewDashboardPage) < prototype(Neos.Neos:Page) {
    @if.render = ${Security.isAuthenticated()}
    // TODO: Maybe redirect to 404 when not authenticated

    head {
        stylesheets = Neos.Fusion:Join {
            mainCSS = Neos.Fusion:Tag {
                tagName = 'link'
                attributes.rel = 'stylesheet'
                attributes.href = Neos.Fusion:ResourceUri {
                    path = 'resource://Flowpack.Neos.WhatsNewDashboard.Editorial/Public/main.css'
                }
            }
        }

        robots = Neos.Fusion:Tag {
            tagName = 'meta'
            attributes.robots = 'noindex, nofollow'
        }
    }

    bodyTag.attributes {
        class.whatsNew = 'whats-new-dashboard__page max-w-[1500px] mx-auto px-10 pt-10 sm:pt-16'
    }

    body >

    body = Neos.Fusion:Join {
        main = Flowpack.Neos.WhatsNewDashboard.Editorial:Layout.Default {
            content = Neos.Fusion:Join {
                header = Flowpack.Neos.WhatsNewDashboard.Editorial:Component.Header {
                    headline = Flowpack.Neos.WhatsNewDashboard.Editorial:Helper.DocumentEditableProperty {
                        node = ${documentNode}
                        property = 'headline'
                    }
                    introText = Flowpack.Neos.WhatsNewDashboard.Editorial:Helper.DocumentEditableProperty {
                        node = ${documentNode}
                        property = 'introText'
                    }
                    menuEntries = Neos.Fusion:Map {
                        items = ${q(documentNode).find('[instanceof Flowpack.Neos.WhatsNewDashboard.Editorial:Content.Section.OneColumn], [instanceof Flowpack.Neos.WhatsNewDashboard.Editorial:Content.Section.TwoColumns]').get()}
                        itemRenderer = ${q(item).property('sectionTitle')}
                    }
                }
                mainContent = Neos.Neos:ContentCollection {
                    nodePath = 'main'
                }
            }
        }
    }
}
