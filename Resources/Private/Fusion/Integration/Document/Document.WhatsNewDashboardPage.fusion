prototype(Flowpack.Neos.WhatsNewEditor.InMyProject:Document.WhatsNewDashboardPage) < prototype(Neos.Neos:Page) {
    @if.render = ${Security.isAuthenticated()}
    // TODO: Maybe redirect to 404 when not authenticated

    head {
        stylesheets = Neos.Fusion:Join {
            mainCSS = Neos.Fusion:Tag {
                tagName = 'link'
                attributes.rel = 'stylesheet'
                attributes.href = Neos.Fusion:ResourceUri {
                    path = 'resource://Flowpack.Neos.WhatsNewEditor.InMyProject/Public/main.css'
                }
            }
        }

        robots = Neos.Fusion:Tag {
            tagName = 'meta'
            attributes.robots = 'noindex, nofollow'
        }
    }

    bodyTag.attributes {
        class.whatsNew = 'whats-new-dashboard__page max-w-[1500px] mx-auto px-10 pt-10 sm:pt-16'
    }

    body >

    body = Neos.Fusion:Join {
        main = Flowpack.Neos.WhatsNewEditor.InMyProject:Layout.Default {
            content = Neos.Fusion:Join {
                header = Flowpack.Neos.WhatsNewEditor.InMyProject:Component.Header {
                    headline = Flowpack.Neos.WhatsNewEditor.InMyProject:Helper.DocumentEditableProperty {
                        node = ${documentNode}
                        property = 'headline'
                    }
                    introText = Flowpack.Neos.WhatsNewEditor.InMyProject:Helper.DocumentEditableProperty {
                        node = ${documentNode}
                        property = 'introText'
                    }
                    menuEntries = Neos.Fusion:Map {
                        items = ${q(documentNode).find('[instanceof Flowpack.Neos.WhatsNewEditor.InMyProject:Content.Section.OneColumn], [instanceof Flowpack.Neos.WhatsNewEditor.InMyProject:Content.Section.TwoColumns]').get()}
                        itemRenderer = ${q(item).property('sectionTitle')}
                    }
                }
                mainContent = Neos.Neos:ContentCollection {
                    nodePath = 'main'
                }
            }
        }
    }
}
