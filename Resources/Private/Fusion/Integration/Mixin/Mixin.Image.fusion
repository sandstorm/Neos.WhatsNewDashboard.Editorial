// Single integration component to render only an image.
// Expects that an image property is present on the node (image mixin is used)
prototype(Flowpack.Neos.WhatsNewDashboard.Editorial:Mixin.Image) < prototype(Neos.Fusion:Component) {
    // default sizes, can be overwritten
    sizes = Flowpack.Neos.WhatsNewDashboard.Editorial:Helper.ImageSizes {
        type = Neos.Fusion:Case {
            contentInTwocolumn {
                condition = ${q(node).parents('[instanceof Flowpack.Neos.WhatsNewDashboard.Editorial:Content.Section.TwoColumns]').get()}
                renderer = 'two-column'
            }
            default {
                condition = true
                renderer = ''
            }
        }
    }
    // default classes, can be overwritten
    imgClasses = 'max-w-full h-auto'
    pictureClasses = 'inline-block'

    renderer = Flowpack.Neos.WhatsNewDashboard.Editorial:Component.Image.Only {
        imageSource = Sitegeist.Kaleidoscope:AssetImageSource {
            asset = ${q(node).property('image')}
        }
        imageUri = Flowpack.Neos.WhatsNewDashboard.Editorial:Helper.ResourceUri {
            resource = ${q(node).property('image').resource}
        }
        fileExtension = ${q(node).property('image').resource.fileExtension}
        alternativeText = ${q(node).property('alternativeText')}

        sizes = ${props.sizes}
        imgClasses = ${props.imgClasses}
        pictureClasses = ${props.pictureClasses}
    }
}
